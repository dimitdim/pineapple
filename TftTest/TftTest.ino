#include <SPI.h>
#define DELAY 0x80

#define ST7735_TFTWIDTH  128
#define ST7735_TFTHEIGHT_18  160
#define ST7735_SWRESET 0x01
#define ST7735_SLPOUT  0x11
#define ST7735_NORON   0x13
#define ST7735_INVOFF  0x20
#define ST7735_DISPON  0x29
#define ST7735_CASET   0x2A
#define ST7735_RASET   0x2B
#define ST7735_RAMWR   0x2C
#define ST7735_COLMOD  0x3A
#define ST7735_MADCTL  0x36
#define ST7735_FRMCTR1 0xB1
#define ST7735_FRMCTR2 0xB2
#define ST7735_FRMCTR3 0xB3
#define ST7735_INVCTR  0xB4
#define ST7735_PWCTR1  0xC0
#define ST7735_PWCTR2  0xC1
#define ST7735_PWCTR3  0xC2
#define ST7735_PWCTR4  0xC3
#define ST7735_PWCTR5  0xC4
#define ST7735_VMCTR1  0xC5
#define ST7735_GMCTRP1 0xE0
#define ST7735_GMCTRN1 0xE1

boolean hwSPI=1;

static const uint8_t PROGMEM
  Rcmd1[] = {                 // Init for 7735R, part 1 (red or green tab)
    15,                       // 15 commands in list:
    ST7735_SWRESET,   DELAY,  //  1: Software reset, 0 args, w/delay
      150,                    //     150 ms delay
    ST7735_SLPOUT ,   DELAY,  //  2: Out of sleep mode, 0 args, w/delay
      255,                    //     500 ms delay
    ST7735_FRMCTR1, 3      ,  //  3: Frame rate ctrl - normal mode, 3 args:
      0x01, 0x2C, 0x2D,       //     Rate = fosc/(1x2+40) * (LINE+2C+2D)
    ST7735_FRMCTR2, 3      ,  //  4: Frame rate control - idle mode, 3 args:
      0x01, 0x2C, 0x2D,       //     Rate = fosc/(1x2+40) * (LINE+2C+2D)
    ST7735_FRMCTR3, 6      ,  //  5: Frame rate ctrl - partial mode, 6 args:
      0x01, 0x2C, 0x2D,       //     Dot inversion mode
      0x01, 0x2C, 0x2D,       //     Line inversion mode
    ST7735_INVCTR , 1      ,  //  6: Display inversion ctrl, 1 arg, no delay:
      0x07,                   //     No inversion
    ST7735_PWCTR1 , 3      ,  //  7: Power control, 3 args, no delay:
      0xA2,
      0x02,                   //     -4.6V
      0x84,                   //     AUTO mode
    ST7735_PWCTR2 , 1      ,  //  8: Power control, 1 arg, no delay:
      0xC5,                   //     VGH25 = 2.4C VGSEL = -10 VGH = 3 * AVDD
    ST7735_PWCTR3 , 2      ,  //  9: Power control, 2 args, no delay:
      0x0A,                   //     Opamp current small
      0x00,                   //     Boost frequency
    ST7735_PWCTR4 , 2      ,  // 10: Power control, 2 args, no delay:
      0x8A,                   //     BCLK/2, Opamp current small & Medium low
      0x2A,  
    ST7735_PWCTR5 , 2      ,  // 11: Power control, 2 args, no delay:
      0x8A, 0xEE,
    ST7735_VMCTR1 , 1      ,  // 12: Power control, 1 arg, no delay:
      0x0E,
    ST7735_INVOFF , 0      ,  // 13: Don't invert display, no args, no delay
    ST7735_MADCTL , 1      ,  // 14: Memory access control (directions), 1 arg:
      0xC8,                   //     row addr/col addr, bottom to top refresh
    ST7735_COLMOD , 1      ,  // 15: set color mode, 1 arg, no delay:
      0x05 },                 //     16-bit color
  Rcmd2red[] = {              // Init for 7735R, part 2 (red tab only)
    2,                        //  2 commands in list:
    ST7735_CASET  , 4      ,  //  1: Column addr set, 4 args, no delay:
      0x00, 0x00,             //     XSTART = 0
      0x00, 0x7F,             //     XEND = 127
    ST7735_RASET  , 4      ,  //  2: Row addr set, 4 args, no delay:
      0x00, 0x00,             //     XSTART = 0
      0x00, 0x9F },           //     XEND = 159
  Rcmd3[] = {                 // Init for 7735R, part 3 (red or green tab)
    4,                        //  4 commands in list:
    ST7735_GMCTRP1, 16      , //  1: Magical unicorn dust, 16 args, no delay:
      0x02, 0x1c, 0x07, 0x12,
      0x37, 0x32, 0x29, 0x2d,
      0x29, 0x25, 0x2B, 0x39,
      0x00, 0x01, 0x03, 0x10,
    ST7735_GMCTRN1, 16      , //  2: Sparkles and rainbows, 16 args, no delay:
      0x03, 0x1d, 0x07, 0x06,
      0x2E, 0x2C, 0x29, 0x2D,
      0x2E, 0x2E, 0x37, 0x3F,
      0x00, 0x00, 0x02, 0x10,
    ST7735_NORON  ,    DELAY, //  3: Normal display on, no args, w/delay
      10,                     //     10 ms delay
    ST7735_DISPON ,    DELAY, //  4: Main screen turn on, no args w/delay
      100 };                  //     100 ms delay

int _width=ST7735_TFTWIDTH;
int _height=ST7735_TFTHEIGHT_18;

void setup(void) {
  Serial.begin(9600);
  Serial.println("Hello! ST7735 TFT Test");
  pinMode(8,OUTPUT);
  pinMode(10,OUTPUT);
  if(hwSPI) {
    SPI.begin();
    SPI.setClockDivider(SPI_CLOCK_DIV4);
    SPI.setBitOrder(MSBFIRST);
    SPI.setDataMode(SPI_MODE0);
  } else {
    pinMode(13, OUTPUT);
    pinMode(11 , OUTPUT);
    digitalWrite(13,LOW);
    digitalWrite(11,LOW);
  }
  digitalWrite(10,LOW);
  pinMode(9, OUTPUT);
  digitalWrite(9, HIGH);
  digitalWrite(9, LOW);
  delay(500);
  digitalWrite(9, HIGH);
  commandList(Rcmd1);
  commandList(Rcmd2red);
  commandList(Rcmd3);
  writecommand(ST7735_MADCTL);
  writedata(0xC0);
  Serial.println("Initialized");
  fillRect(0, 0, 128, 160, 0x07FF);
}

void loop() {
  Serial.println("Cyan");
  fillRect(0, 0, 128, 160, 0x07FF);
  
  Serial.println("Red Dot");
  fillRect(64, 80, 1, 1, 0xF800);
  delay(500);
  
  Serial.println("Black");
  fillRect(0, 0, 128, 160, 0x0000);
  delay(500);
}

void fillRect(int16_t x,int16_t y,int16_t w,int16_t h,uint16_t color) {
  if((x >= _width) || (y >= _height)) return;
  if((x + w - 1) >= _width)  w = _width  - x;
  if((y + h - 1) >= _height) h = _height - y;
  writecommand(ST7735_CASET); // Column addr set
  writedata(0x00);
  writedata(x);     // XSTART
  writedata(0x00);
  writedata(x+w-1);     // XEND
  writecommand(ST7735_RASET); // Row addr set
  writedata(0x00);
  writedata(y);     // YSTART
  writedata(0x00);
  writedata(y+h-1);     // YEND
  writecommand(ST7735_RAMWR); // write to RAM
  uint8_t hi = color >> 8, lo = color;
  digitalWrite(8,HIGH);
  digitalWrite(10,LOW);
  for(y=h; y>0; y--) {
    for(x=w; x>0; x--) {
      spiwrite(hi);
      spiwrite(lo);
    }
  }
  digitalWrite(10,HIGH);
}

void writecommand(uint8_t c) {
  digitalWrite(8,LOW);
  digitalWrite(10,LOW);
  spiwrite(c);
  digitalWrite(10,HIGH);
}

void writedata(uint8_t c) {
  digitalWrite(8,HIGH);
  digitalWrite(10,LOW);
  spiwrite(c);
  digitalWrite(10,HIGH);
}

void spiwrite(uint8_t c) {
  if(hwSPI) SPI.transfer(c);
  else {
    for(uint8_t bit = 0x80; bit; bit >>= 1) {
      if(c & bit) digitalWrite(11,HIGH);
      else        digitalWrite(11,LOW);
      digitalWrite(13, HIGH);
      digitalWrite(13, LOW);
    }
  }
}

void commandList(const uint8_t *addr) {

  uint8_t  numCommands, numArgs;
  uint16_t ms;

  numCommands = pgm_read_byte(addr++);   // Number of commands to follow
  while(numCommands--) {                 // For each command...
    writecommand(pgm_read_byte(addr++)); //   Read, issue command
    numArgs  = pgm_read_byte(addr++);    //   Number of args to follow
    ms       = numArgs & DELAY;          //   If hibit set, delay follows args
    numArgs &= ~DELAY;                   //   Mask out delay bit
    while(numArgs--) {                   //   For each argument...
      writedata(pgm_read_byte(addr++));  //     Read, issue argument
    }

    if(ms) {
      ms = pgm_read_byte(addr++); // Read post-command delay time (ms)
      if(ms == 255) ms = 500;     // If 255, delay for 500 ms
      delay(ms);
    }
  }
}
